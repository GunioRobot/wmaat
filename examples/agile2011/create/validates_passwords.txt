** Settings **
Documentation   A valid password ...

** Test Cases **

A Password With No Letters      Is Rejected 1234!@$^
A Password With No Digits       Is Rejected abcd!@$^
A Password With No Punctuation                  Is Rejected abcd1234
A Password That Is One Character Less Than Minimum Length    Is Rejected !2c45
A Password That Is One Character More Than Maximum Length     Is Rejected !2c45678901234567

A Password That Is Minimum Length  Is Accepted !2c456
A Password That Is Maximum Length  Is Accepted !2c4567890123456



** Settings **
Library         OperatingSystem
Library         SeleniumLibrary
Suite Setup     Start Selenium Session
Suite TearDown  End Selenium Session



# These next two keywords are about accepting and rejecting
# passwords. I would move them to a resource file
# named _password_keywords.txt.
# See the "6-change-technology" folder an example.

** Keywords **
Is Accepted ${password}
    Create User With Password ${password}
    Status Should Be Account Created

Is Rejected ${password}
    Create User With Password ${password}
    Status Should Be Invalid Password


# These next two keywords are about attempting to create an account
# and observing the result. I would move them to a resource file.
# Because they work through the application's web interface,
# I might call the file _web_keywords.txt. Or I might call it
# _create_account.txt, or ...  What would /you/ call it?
# See the "6-change-technology" folder for an example.

** Keywords **
Create User With Password ${password}
    Go To   http://localhost:4567/create
    Input Text  username    fred
    Input Text  password    ${password}
    Submit Form

Status Should Be ${expected_status}
    Element Should Contain  status  ${expected_status}

# These next two keywords start and stop a selenium server so that
# we can access the web app. I would move these keywords either to
# the _web_keywords.txt file, or maybe to a file of their own,
# called _selenium_session.txt.

Start Selenium Session
    Start Selenium Server
    Open Browser  http://localhost:4567
    Set Selenium Speed  1
    Maximize Browser Window

End Selenium Session
    Close Browser
    Stop Selenium Server
