** Settings **
Documentation   The create command validates passwords.

Documentation   Definition of valid password:
...             A valid password is from 6 to 16 characters
...             long, and contains at least one letter, at
...             least one digit, and at least one
...             punctuation character.

Documentation   The system's responsibilities:
...             - If the submitted password is valid,
...               the create command creates the account
...               and reports "Account Created".
...             - If the submitted password is invalid,
...               the create command refuses the account
...               and reports "Invalid Password"




** Test Cases **
The create command validates passwords
    Rejects Password ${aPasswordWithNoLetters}
    Rejects Password ${aPasswordWithNoDigits}
    Rejects Password ${aPasswordWithNoPunctuation}
    Rejects Password ${aTooShortPassword}
    Accepts Password ${aMinimumLengthPassword}
    Accepts Password ${aMaximumLengthPassword}
    Rejects Password ${aTooLongPassword}




** Variables **
${aPasswordWithNoLetters}       1234!@$^
${aPasswordWithNoDigits}        abcd!@$^
${aPasswordWithNoPunctuation}   abcd1234
${aTooShortPassword}            !2c45
${aMinimumLengthPassword}       !2c456
${aMaximumLengthPassword}       !2c4567890123456
${aTooLongPassword}             !2c45678901234567




** Keywords **
Create Account ${username} ${password}
    ${status}=  Run  ruby app/cli.rb create ${username} ${password}
    Set Test Variable   ${status}

Status Should Be ${expected_status}
    Should Be Equal  ${status}  ${expected_status}

Accepts Password ${valid_password}
    Create Account arbitraryUserName ${valid_password}
    Status Should Be Account Created

Rejects Password ${invalid_password}
    Create Account arbitraryUserName ${invalid_password}
    Status Should Be Invalid Password




** Settings **
Library         OperatingSystem
